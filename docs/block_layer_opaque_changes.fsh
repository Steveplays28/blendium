// resources/assets/sodium/shaders/blocks/block_layer_opaque.fsh

#version 330 core

#import <sodium:include/fog.glsl>

in vec3 v_ColorModulator; // The interpolated vertex color
in vec2 v_TexCoord; // The interpolated block texture coordinates

in float v_FragDistance; // The fragment's distance from the camera

in float v_MaterialMipBias;
in float v_MaterialAlphaCutoff;

uniform sampler2D u_BlockTex; // The block atlas texture

uniform vec4 u_FogColor; // The color of the shader fog
uniform float u_FogStart; // The starting position of the shader fog
uniform float u_FogEnd; // The ending position of the shader fog

out vec4 out_FragColor; // The output fragment for the color framebuffer



// Custom uniform variables
uniform int u_Far; // The view distance



void main() {
    vec4 diffuseColor = texture(u_BlockTex, v_TexCoord, v_MaterialMipBias);

#ifdef USE_FRAGMENT_DISCARD
    if (diffuseColor.a < v_MaterialAlphaCutoff) {
        discard;
    }
#endif

    // Modulate the color (used by ambient occlusion and per-vertex colouring)
    diffuseColor.rgb *= v_ColorModulator;

    out_FragColor = _linearFog(diffuseColor, v_FragDistance, u_FogColor, u_FogStart, u_FogEnd);



	// Blend the alpha of the blocks
	float far = u_Far * 16.0;
	out_FragColor.a *= 1.0 - smoothstep(0.4 * far, far, v_FragDistance);
}
